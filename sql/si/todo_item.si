DATABASE ExampleDatabase
PACKAGE com.example.db
SERVER ExampleServer
SCHEMA ToDoList_App

TABLE ToDo_Item
   ID               SEQUENCE
   TodoList_ID      INT     //This is a foreign key to the ToDoList table
   ItemName         CHAR(255)
   ItemDescription  CLOB
   LastUpdated      TIMESTAMP

//This define ID as the Primary Key
KEY PKEY PRIMARY
    ID

LINK ToDoList TodoList_ID

//We do a normal Insert and Update without a Returning here, to test the regular generaion
//We do an InsertReturning and UpdateReturning in the ToDoList table to test that generation there
PROC Insert
PROC Update
PROC SelectOne
PROC DeleteOne

//The SelectBy function automatically creates
//a SELECT query using the given fields as the
//WHERE clause
PROC SelectBy TodoList_ID RETURNING ID ItemName ItemDescription LastUpdated


PROC UpdateBy ItemName SET ItemDescription 

PROC GetItemsWithListNameForID
INPUT
    ID              =
OUTPUT
    ListName        CHAR(255)
    ItemName        =
    ItemDescription =
SQLCODE
    SELECT  
            l.ListName,
            i.ItemName,
            i.ItemDescription
    FROM
            ToDoList_App.ToDoList l 
            INNER JOIN
            ToDoList_App.ToDo_Item i
            ON l.ID = i.TodoList_ID
    WHERE i.ID =  :ID
ENDCODE


PROC SelectWithDynamicQuery
INPUT
    ID              =
OUTPUT
    ItemName        =
    ItemDescription =
    LastUpdated     =
SQLCODE
    SELECT 
        ItemName,
        ItemDescription,
        LastUpdated
    FROM
        ToDoList_App.ToDo_Item i
    WHERE
        i.ID = :ID
    AND
        ItemName IN (&ItemNamesList(1024))
ENDCODE